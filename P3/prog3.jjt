options
{
    MULTI = true;
    STATIC = false;
}
PARSER_BEGIN(prog3)
class prog3
{
    public static void main(String args[])
    {
        System.out.println("Enter an applied Lambda Expression");
        prog3 obj = new prog3(System.in);
        ASTStart t1 = null;
        try
        {
            t1 = obj.Start();
            System.out.println("The abstract syntax tree:");
            ((SimpleNode)t1.jjtGetChild(0)).dump("");
        }
        catch(Exception e)
        {
            System.out.println("Oops.");
            System.out.println(e.getMessage());
            e.printStackTrace();
        }
        
        System.out.println("Enter the variable to be substituted:");
        String s;
        String s1;
        Scanner sc = new Scanner(System.in);
        s1 = sc.next();
        s = s1.charAt(0) + "";
        
        System.out.println("Enter the substituting applied Lambda expression:");
        prog3 obj1 = new prog3(System.in);
        ASTStart t2 = null;
        try
        {
            t2 = obj1.Start();
            System.out.println("The abstract syntax tree:");
            ((SimpleNode)t2.jjtGetChild(0)).dump("");
        }
        catch(Exception e)
        {
            System.out.println("Oops.");
            System.out.println(e.getMessage());
            e.printStackTrace();
        }
        System.out.println("Variale s1 is: "+ s1);
        System.out.println("Variale s is: "+ s);
        if(t1 != null){
            SimpleNode subNode = ((SimpleNode)t1.jjtGetChild(0)).substitute(s1,(SimpleNode)t2.jjtGetChild(0));
            System.out.println("The substitution result:");
            subNode.dump("");
        }

        System.out.println(">>> Lambda Expression Evaluator <<<");
        System.out.println("Enter an applied Lambda expression:");
        prog3 obj2 = new prog3(System.in);
        ASTStart t3 = null;
        try
        {
            t3 = obj2.Start();
            SimpleNode reducedNode = ((SimpleNode)t3.jjtGetChild(0)).applicativeOrderEvaluate();
            System.out.println("\nThe applicative order evaluation result:");
            reducedNode.dump("");
        }
        catch(Exception e)
        {
            System.out.println("Oops.");
            System.out.println(e.getMessage());
            e.printStackTrace();
        }
    }
}
PARSER_END(prog3)
SKIP:
{
        " "
|       "\n"
|       "\t"
|       "\r"
}
TOKEN:
{
        <Lambda: "L">
|       <var: ["a"-"z","_"](["a"-"z","0"-"9","_"])*>
|       <num: (["0"-"9"])+>
}

ASTStart Start():
{       }
{
        Expression()
        {return jjtThis;}
}

void Expression() #void:
{       }
{
        Variable()
|       Number()
|       "+" #add(0)
|       "-" #sub(0)
|       "*" #mul(0)
|       "/" #div(0)
|       "%" #mod(0)
|       Appl()
|       Abstraction()
}
void Number():
{ Token t; }
{
    t = <num>
    {  jjtThis.setName(t.image); }
}
void Variable():
{ Token t; }
{
    t = <var>
    { jjtThis.setName(t.image); }
}
void Appl() #void:
{       }
{
    "(" Expression() (Expression() #appl(2))* ")"
}
void Body() #void:
{   }
{
    "." Application()
|   Variable() Body() #lamb(2)
}
void Application() #void:
{   }
{
    Expression() (LOOKAHEAD(2)Expression() #appl(2))*
}
void Abstraction() #void:
{   }
{
    <Lambda> Variable() Body() #lamb(2)
}